apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs.kotlin"

apply plugin: 'com.android.application'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'com.google.firebase.crashlytics'


def secretsPropertiesFile = rootProject.file("secrets.properties");
def secretsProperties = new Properties()
secretsProperties.load(new FileInputStream(secretsPropertiesFile))

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.lucianghimpu.matchmefy"
        manifestPlaceholders = [
                appAuthRedirectScheme: 'com.lucianghimpu.matchmefy'
        ]
        buildConfigField("String", "spotify_client_id", secretsProperties['spotify_client_id'])
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 4
        versionName "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField("String", "matchmefy_api_url", secretsProperties['matchmefy_api_url_dev'])
        }

        release {
            buildConfigField("String", "matchmefy_api_url", secretsProperties['matchmefy_api_url_live'])
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    android.dataBinding {
        enabled true
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
    maven { url "https://jitpack.io" }
}

dependencies {

    def kotlinCoroutineVersion = "1.3.3"
    def okhttp3version = "4.3.1"
    def gsonVersion = "2.8.6"
    def retrofit2version = "2.7.1"
    def koin_version = "2.0.1"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Koin for Android Architecture ViewModel
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutineVersion"

    // okhttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp3version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3version"

    // gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit2version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    // secure shared preferences
    implementation "androidx.security:security-crypto:1.0.0-alpha02"

    // circle image view
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    // view pager
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // dots indicator
    implementation 'com.tbuonomo.andrui:viewpagerdotsindicator:4.1.2'

    // flexbox layout
    implementation 'com.google.android:flexbox:2.0.1'

    // oauth2 library
    implementation 'net.openid:appauth:0.7.1'

    // reflection
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // firebase
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'

    // timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // merlin
    implementation 'com.novoda:merlin:1.2.0'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
}
